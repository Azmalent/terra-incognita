//file:noinspection SpellCheckingInspection
plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16, 6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

group = "azmalent"
version = "${minecraft_version}-${mod_version}"

base {
    archivesName = modid
}

java{
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'parchment', version: "${mappings_version}-${minecraft_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven { // JEI
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { // TOP
        name 'tterrag'
        url 'https://maven.tterrag.com/'
    }
    maven { // Quark
        name "blamejared"
        url "https://maven.blamejared.com/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("curse.maven:cuneiform-419176:${cuneiform_file_id}")
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${minecraft_version}-${terrablender_version}")

    runtimeOnly fg.deobf("curse.maven:rubidium-574856:${rubidium_file_id}")
    runtimeOnly fg.deobf("curse.maven:cyanide-forge-541676:${cyanide_file_id}")

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    compileOnly fg.deobf("curse.maven:jade-api-324717:${jade_api_file_id}")
    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_file_id}")

    implementation fg.deobf("curse.maven:the-one-probe-245211:3671753") //not yet on maven

    runtimeOnly fg.deobf("vazkii.autoreglib:AutoRegLib:1.7-54.112")
    implementation fg.deobf("vazkii.quark:Quark:${quark_version}.${quark_build_number}")

    implementation fg.deobf("curse.maven:farmers-delight-398521:${farmers_delight_file_id}")

    compileOnly fg.deobf("vazkii.botania:Botania:${minecraft_version}-${botania_version}:api")
}

mixin {
    add sourceSets.main, "${modid}.mixins.refmap.json"
    config "${modid}.mixins.json"
}

processResources {
    var replaceProperties = [
            'mod_name': mod_name, 'mod_version': project.version,
            'minecraft_version': minecraft_version, 'minecraft_version_range': minecraft_version_range,
            'fml_min': fml_min, 'forge_version': forge_version,
            'cuneiform_version': cuneiform_version,
            'terrablender_version': terrablender_version,
            'jei_version': jei_version,
            'quark_version': quark_version
    ]

    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties + [project: project]
    }

/*    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'mod_name': mod_name, 'mod_version': project.version,
                'minecraft_version': minecraft_version, 'minecraft_version_range': minecraft_version_range,
                'fml_min': fml_min, 'forge_version': forge_version,
                'cuneiform_version': cuneiform_version,
                'terrablender_version': terrablender_version,
                'jei_version': jei_version,
                'quark_version': quark_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

    duplicatesStrategy 'include'*/
}

jar {
    manifest {
        attributes([
                "Specification-Title": modid,
                "Specification-Vendor": "azmalent",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor": "azmalent",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),

                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                "MixinConfigs": "${modid}.mixins.json",
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true'
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all,-deprecation,-processing"
}
